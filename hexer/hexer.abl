MODULE   Hexer
TITLE   'Hexer Game'


" HexerPAL   DEVICE  'PALCE29M16'


" This file contains the pinout for the hexer game described in the homework.
" This version matches the new PCB.  All inputs are active low and all outputs
" are active high (except ClkOut which could be active high or low).
"
" The board and LED assignments look like
"                1
"              /  \
"      11----10-----8-----9
"     /  \  /  \  /  \  /  \
"   13----12-----2----15----14
"     \  /  \  /  \  /  \  /
"       7-----6-----4-----5
"              \  /
"                3
"
" Where the color/shape/LED/action combinations are:
"   white /  hexagon / 10,  8, 15,  4,  6, 12             / invert LEDs
"   black /  hexagon /  2,  8,  9, 14,  5,  4             / rotate clockwise once
"    blue /  hexagon / 11, 10,  2,  6,  7, 13             / rotate counterclockwise once
"     red / triangle /  1,  8, 15,  5,  4,  6,  7, 12, 10 / rotate clockwise once
"   green / triangle /  3,  6, 12, 11, 10,  8,  9, 15,  4 / rotate counterclockwise once
"

" Revision History
"    26 October 09   Glen George       initial revision (from 10/27/07 version
"                                         of hexer0.pds
"    31 October 09   Glen George       fixed minor syntax error
"    25 October 16   Tim Menninger     Implemented Hexer game



"------------------------------------------------------------------------------------------------------------------------------
" pins
"------------------------------------------------------------------------------------------------------------------------------

Clock      pin   11;               "input   clock and random reset
!BlackSw   pin   2;               "input   Black switch input/Manual Reset 1 (active low)
Out1       pin   3  ISTYPE 'reg'; "output  current state of position 1 (active high)
Out11      pin   4  ISTYPE 'reg'; "output  current state of position 11 (active high)
Out10      pin   5  ISTYPE 'reg'; "output  current state of position 10 (active high)
Out8       pin   6  ISTYPE 'reg'; "output  current state of position 8 (active high)
Out12      pin   7  ISTYPE 'reg'; "output  current state of position 12 (active high)
Out2       pin   8  ISTYPE 'reg'; "output  current state of position 2 (active high)
Out9       pin   9  ISTYPE 'reg'; "output  current state of position 9 (active high)
Out13      pin  10  ISTYPE 'reg'; "output  current state of position 13 (active high)
!BlueSw    pin  41;               "input   Blue switch input/Manual Reset 0 (active low)
"GND       pin  12;                supply  GND
!WhiteSw   pin  43;               "input   White switch input/Manual Reset (active low)
!RedSw     pin  44;               "input   Red switch input (active low)
Out14      pin  15  ISTYPE 'reg'; "output  current state of position 14 (active high)
Out7       pin  16  ISTYPE 'reg'; "output  current state of position 7 (active high)
Out6       pin  17  ISTYPE 'reg'; "output  current state of position 6 (active high)
Out5       pin  18  ISTYPE 'reg'; "output  current state of position 5 (active high)
ClkOut     pin  19  ISTYPE 'com'; "output  clock output - used to clock an input
Out15      pin  20  ISTYPE 'reg'; "output  current state of position 15 (active high)
Out4       pin  21  ISTYPE 'reg'; "output  current state of position 4 (active high)
Out3       pin  22  ISTYPE 'reg'; "output  current state of position 3 (active high)
!GreenSw   pin  42;               "input   Green switch input/Manual Reset (active low)
"VCC       pin  24;                supply  internal VCC




"------------------------------------------------------------------------------------------------------------------------------
" Some constants to improve readability
"------------------------------------------------------------------------------------------------------------------------------

" Put output bits in a list so we can easily set all of their clocks at once
OutputBits  = [ Out15, Out14, Out13, Out12, Out11, Out10, Out9, Out8, Out7, Out6, Out5, Out4, Out3, Out2, Out1 ];

" Shorthand to know when no inputs are active
NoSw        = !BlackSw & !BlueSw & !GreenSw & !RedSw & !GreenSw;

" Define a variable that represents each switch as being the only one pressed
Black       =  BlackSw & !BlueSw & !GreenSw & !RedSw & !WhiteSw;
Blue        = !BlackSw &  BlueSw & !GreenSw & !RedSw & !WhiteSw;
Green       = !BlackSw & !BlueSw &  GreenSw & !RedSw & !WhiteSw;
Red         = !BlackSw & !BlueSw & !GreenSw &  RedSw & !WhiteSw;
White       = !BlackSw & !BlueSw & !GreenSw & !RedSw &  WhiteSw;

" Need to know if all outs are zero for a boundary case in our LFSR
BitSet      = Out15 # Out14 # Out13 # Out12 # Out11 # Out10 # Out9 # Out8 # Out7 # Out6 # Out5 # Out4 # Out3 # Out2 # Out1;

" Define constants that will be true when resetting
ManRS       = GreenSw & WhiteSw;
ManRS0      = ManRS & !BlackSw &  BlueSw & !RedSw;
ManRS1      = ManRS &  BlackSw & !BlueSw & !RedSw;
RandRS      = NoSw;

" Define a 15-bit manual shifter for manual resets.  Order matches such that bits shift in rows,
" i.e., 1, 11, 10, 8, 9, 13, 12, 2, 15, 14, 7, 6, 4, 5, 3
Man1        = ManRS1; " If there's a clock, it's because another button was pushed
Man2        = ManRS & (BlackSw # BlueSw) & Out12;
Man3        = ManRS & (BlackSw # BlueSw) & Out5;
Man4        = ManRS & (BlackSw # BlueSw) & Out6;
Man5        = ManRS & (BlackSw # BlueSw) & Out4;
Man6        = ManRS & (BlackSw # BlueSw) & Out7;
Man7        = ManRS & (BlackSw # BlueSw) & Out14;
Man8        = ManRS & (BlackSw # BlueSw) & Out10;
Man9        = ManRS & (BlackSw # BlueSw) & Out8;
Man10       = ManRS & (BlackSw # BlueSw) & Out11;
Man11       = ManRS & (BlackSw # BlueSw) & Out1;
Man12       = ManRS & (BlackSw # BlueSw) & Out13;
Man13       = ManRS & (BlackSw # BlueSw) & Out9;
Man14       = ManRS & (BlackSw # BlueSw) & Out15;
Man15       = ManRS & (BlackSw # BlueSw) & Out2;

" Define a 15-bit LFSR for random resets. This will be 1-indexed to match the LED indices
LFSR1       = RandRS & !BitSet # (Out14 $ Out15); " Linear feedback part
LFSR2       = RandRS & Out1;
LFSR3       = RandRS & Out2;
LFSR4       = RandRS & Out3;
LFSR5       = RandRS & Out4;
LFSR6       = RandRS & Out5;
LFSR7       = RandRS & Out6;
LFSR8       = RandRS & Out7;
LFSR9       = RandRS & Out8;
LFSR10      = RandRS & Out9;
LFSR11      = RandRS & Out10;
LFSR12      = RandRS & Out11;
LFSR13      = RandRS & Out12;
LFSR14      = RandRS & Out13;
LFSR15      = RandRS & Out14;




"------------------------------------------------------------------------------------------------------------------------------
EQUATIONS
"------------------------------------------------------------------------------------------------------------------------------

" The fun begins

" Enable outputs on the clock out pin
ClkOut.OE       = 1;

" Use global clock for all of our registered outputs
OutputBits.CLK  = Clock;






"
" Setting Outputs
"
"
" ClkOut
"
" Clockout signal will be active whenever a switch is active.  However, we
" need to know when, if in manual reset mode, a 0 or a 1 is shifted in.  Thus,
" we will consider the system as if no switch is active if we are in ManRS
" mode (when actually, two switches are active)
"
ClkOut = !NoSw & !ManRS;

"
" LEDS
"
" Normal Operation:
" Equations here set all of the lights based on the rules of the game.  In this,
" an inversion happens exactly once per triggered input, and each rotation
" rotates exactly one position per triggered input.
"     Black switch rotates outs 2, 8, 9, 14, 5, and 4, (with 2->8, ..., 4->2)
"     Blue switch rotates outs 2, 10, 11, 13, 7, and 6, (with 2->10, ..., 6->2)
"     Green switch rotates outs 3, 4, 15, 9, 8, 10, 11, 12, and 6, (with 3->4, ..., 6->3)
"     Red switch rotates outs 1, 8, 15, 5, 4, 6, 7, 12, and 10, (with 1->8, ..., 10->1)
"     White switch inverts outs 4, 6, 12, 10, 8, and 15, (on->off, off->on)
"
" Random Reset:
" This only acknowledges the random reset switch if no other switches are
" pushed.  The reason for this is because the Clock pin mirrors the previous
" ClkOut, so we cannot look at that pin alone.
" Note that LSFR's have a stuck state when all outputs are 0.  Thus, when
" resetting, we must set lowest bit if all outputs are low.
"
" Manual Reset:
" This is only acknowledged if both manual reset pins are active.  Because both
" pins are overloaded, pressing green and white simultaneously will trick the
" system into thinking it is being manually reset.  This does not start the
" outputs at any known value.  When resetting, all 15 LEDs must be defined,
" or else the starting state of the game is undefined.
"
" Error Handling:
" Multiple active inputs, if not otherwise explicitly defined, will cause
" undefined behavior.
"

"----------------------------------------------------------------------------------------------------------------------
"            Reset     ""   Black switch  ""   Blue switch  ""  Green switch   ""   Red switch  ""    White switch  "";
"----------------------------------------------------------------------------------------------------------------------
Out1  = LFSR1  # Man1  ""                 ""                ""                 "" # Red & Out10 ""                  "";
Out2  = LFSR2  # Man2  "" # Black & Out4  "" # Blue & Out6  ""                 ""               ""                  "";
Out3  = LFSR3  # Man3  ""                 ""                "" # Green & Out6  ""               ""                  "";
Out4  = LFSR4  # Man4  "" # Black & Out5  ""                "" # Green & Out3  "" # Red & Out5  "" # White & !Out4  "";
Out5  = LFSR5  # Man5  "" # Black & Out14 ""                ""                 "" # Red & Out15 ""                  "";
Out6  = LFSR6  # Man6  ""                 ""                ""                 "" # Red & Out4  "" # White & !Out6  "";
Out7  = LFSR7  # Man7  ""                 "" # Blue & Out13 ""                 "" # Red & Out6  ""                  "";
Out8  = LFSR8  # Man8  "" # Black & Out2  ""                "" # Green & Out9  "" # Red & Out1  "" # White & !Out8  "";
Out9  = LFSR9  # Man9  "" # Black & Out8  ""                "" # Green & Out15 ""               ""                  "";
Out10 = LFSR10 # Man10 ""                 "" # Blue & Out2  "" # Green & Out8  "" # Red & Out12 "" # White & !Out10 "";
Out11 = LFSR11 # Man11 ""                 "" # Blue & Out10 "" # Green & Out10 ""               ""                  "";
Out12 = LFSR12 # Man12 ""                 ""                "" # Green & Out11 "" # Red & Out7  "" # White & !Out12 "";
Out13 = LFSR13 # Man13 ""                 "" # Blue & Out11 ""                 ""               ""                  "";
Out14 = LFSR14 # Man14 "" # Black & Out10 ""                ""                 ""               ""                  "";
Out15 = LFSR15 # Man15 ""                 ""                "" # Green & Out4  "" # Red & Out8  "" # White & !Out15 "";
"----------------------------------------------------------------------------------------------------------------------



END  Hexer
